import org.tiqr.authenticator.buildsrc.Versions
import org.tiqr.authenticator.buildsrc.Libs

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion Versions.compileSdkVersion
    buildToolsVersion Versions.buildToolsVersion

    defaultConfig {
        minSdkVersion Versions.minSdkVersion
        targetSdkVersion Versions.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        buildConfigField "String", "TOKENEXCHANGE_URL", "\"https://tx.tiqr.org/tokenexchange/?appId=tiqr\""

        buildConfigField "String", "TOKEN_EXCHANGE_BASE_URL", "\"https://tx.tiqr.org/\""
        buildConfigField "String", "TOKEN_EXCHANGE_APP_ID", "\"tiqr\""

        buildConfigField "String", "BASE_URL", "\"https://demo.tiqr.org\""
        buildConfigField "String", "PROTOCOL_VERSION", "\"2\""

        buildConfigField "String", "TIQR_ENROLL_SCHEME", "\"${project.schemeEnroll}://\""
        buildConfigField "String", "TIQR_AUTH_SCHEME", "\"${project.schemeAuth}://\""

        kapt {
            correctErrorTypes = true
            useBuildCache = true
            javacOptions {
                option("-Xmaxerrs", 1000)
            }
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
                arg("room.incremental", "true")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

androidExtensions {
    features = ["parcelize"]
}

dependencies {
    implementation Libs.Kotlin.stdlib
    implementation Libs.Kotlin.Coroutines.core
    implementation Libs.Kotlin.Coroutines.android
    implementation Libs.AndroidX.core
    implementation Libs.AndroidX.Lifecycle.liveData
    implementation Libs.AndroidX.Lifecycle.viewmodel
    implementation Libs.Google.material

    api Libs.Dagger.dagger
    kapt Libs.Dagger.compiler

    api Libs.OkHttp.okhttp
    api Libs.OkHttp.logging

    api Libs.Retrofit.retrofit
    implementation Libs.Retrofit.converterMoshi
    implementation Libs.Retrofit.converterScalars

    api Libs.Moshi.moshi
    kapt Libs.Moshi.codegen

    api Libs.AndroidX.Room.runtime
    implementation Libs.AndroidX.Room.ktx
    implementation Libs.AndroidX.Room.sqlite
    kapt Libs.AndroidX.Room.compiler

    api Libs.Timber.timber

    androidTestImplementation Libs.Kotlin.Coroutines.test
    testImplementation Libs.Testing.jUnit
    androidTestImplementation Libs.Testing.jUnitAndroidx
    androidTestImplementation Libs.Testing.epsresso
}
