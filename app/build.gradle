import org.tiqr.authenticator.buildsrc.Libs
import org.tiqr.authenticator.buildsrc.Versions

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    compileSdkVersion Versions.compileSdkVersion
    buildToolsVersion Versions.buildToolsVersion

    defaultConfig {
        applicationId "org.tiqr.authenticator"
        versionCode 18 // TODO: check latest version and bump
        versionName "4.0.0"  // TODO: check versions, probably 4.0.0

        minSdkVersion Versions.minSdkVersion
        targetSdkVersion Versions.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders.schemeEnroll = project.schemeEnroll
        manifestPlaceholders.schemeAuth = project.schemeAuth

        resConfigs "nl", "en" // TODO: add supported languages

        dataBinding {
            enabled = true
        }

        kapt {
            correctErrorTypes = true
            useBuildCache = true
            javacOptions {
                option("-Xmaxerrs", 1000)
            }
        }
    }

    lintOptions {
        // TODO: reset lintOptions to default
        //checkReleaseBuilds false
        abortOnError false
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    implementation Libs.Kotlin.stdlib
    implementation Libs.Kotlin.Coroutines.android
    implementation Libs.Kotlin.Coroutines.playServices

    implementation Libs.AndroidX.activity
    implementation Libs.AndroidX.core
    implementation Libs.AndroidX.browser
    implementation Libs.AndroidX.constraintLayout
    implementation Libs.AndroidX.futures
    implementation Libs.AndroidX.Camera.core
    implementation Libs.AndroidX.Camera.camera2
    implementation Libs.AndroidX.Camera.view
    implementation Libs.AndroidX.Camera.lifecycle
    implementation Libs.AndroidX.Navigation.fragment
    implementation Libs.AndroidX.Navigation.ui
    implementation Libs.AndroidX.Lifecycle.liveData
    implementation Libs.AndroidX.Lifecycle.commonJ8
    implementation Libs.Google.material
    implementation Libs.Google.Firebase.vision
    implementation Libs.Google.Firebase.barcode

    implementation project(':data')

    implementation Libs.Dagger.android
    kapt Libs.Dagger.compiler

    implementation Libs.Permission.kPermissions
    implementation Libs.betterLink

    testImplementation Libs.Testing.jUnit
    androidTestImplementation Libs.Testing.jUnitAndroidx
    androidTestImplementation Libs.Testing.epsresso
}

// Disable analytics
configurations.all {
    exclude group: 'com.google.firebase', module: 'firebase-core'
    exclude group: 'com.google.firebase', module: 'firebase-analytics'
    exclude group: 'com.google.firebase', module: 'firebase-measurement-connector'
}

apply plugin: 'com.google.gms.google-services'