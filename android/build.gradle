apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "org.tiqr.authenticator"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 28
        versionName "3.0.10"
        multiDexEnabled true
    }

    dataBinding {
        enabled = true
    }

    androidExtensions {
        experimental = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    compileOptions {
        // Flag to enable support for the new language APIs.
        // This is required to keep on using the latest version of ZXing: https://github.com/zxing/zxing/issues/1170#issuecomment-555860273
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

configurations {
    compileClasspath {
        // Force to 1.0.2 because 1.1.0 has a webview-bug
        resolutionStrategy.force 'androidx.appcompat:appcompat:1.0.2'
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.10'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "androidx.core:core-ktx:1.3.0"
    implementation "androidx.multidex:multidex:2.0.1"


    // Dependency injection
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    //Support
    implementation 'androidx.appcompat:appcompat:1.0.2'

    // Push notification
    implementation 'com.google.firebase:firebase-messaging:20.3.0'
    implementation 'com.github.bumptech.glide:glide:4.10.0'


    implementation project(':shared')

    implementation 'com.journeyapps:zxing-android-embedded:4.1.0'
}

configurations.all {
    exclude group: 'com.google.firebase', module: 'firebase-core'
    exclude group: 'com.google.firebase', module: 'firebase-analytics'
    exclude group: 'com.google.firebase', module: 'firebase-measurement-connector'
}

apply plugin: 'com.google.gms.google-services'
